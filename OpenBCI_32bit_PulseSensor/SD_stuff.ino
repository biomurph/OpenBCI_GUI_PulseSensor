
#define BLOCK_5MIN  11000
#define BLOCK_15MIN  33000
#define BLOCK_30MIN  66000
#define BLOCK_1HR  131000
#define BLOCK_2HR  261000
#define BLOCK_4HR  521000
#define BLOCK_12HR  1561000
#define BLOCK_24HR  3122000

#define OVER_DIM 20 // make room for up to 20 write-time overruns

uint32_t BLOCK_COUNT;
SdFile openfile;  // want to put this before setup...
Sd2Card card(&OBCI.spi,SD_SS);// SPI needs to be init'd before here
SdVolume volume;
SdFile root;
uint8_t* pCache;      // array that points to the block buffer on SD card
uint32_t MICROS_PER_BLOCK = 2000; // block write longer than this will get flaged
uint32_t bgnBlock, endBlock; // file extent bookends
int byteCounter = 0;    // used to hold position in cache
//int blockCounter;       // count up to BLOCK_COUNT with this
boolean openvol;
boolean cardInit = false;
boolean fileIsOpen = false;

struct {
  uint32_t block;   // holds block number that over-ran
  uint32_t micro;  // holds the length of this of over-run
} over[OVER_DIM];
uint32_t overruns;      // count the number of overruns
uint32_t maxWriteTime;  // keep track of longest write time
uint32_t minWriteTime;  // and shortest write time
uint32_t t;        // used to measure total file write time

byte fileTens, fileOnes;  // enumerate succesive files on card and store number in EEPROM
char currentFileName[] = "OBCI_00.TXT"; // file name will enumerate in hex 00 - FF
prog_char elapsedTime[] PROGMEM = {"\n%Total time mS:\n"};  // 17
prog_char minTime[] PROGMEM = {  "%min Write time uS:\n"};  // 20
prog_char maxTime[] PROGMEM = {  "%max Write time uS:\n"};  // 20
prog_char overNum[] PROGMEM = {  "%Over:\n"};               //  7
prog_char blockTime[] PROGMEM = {  "%block, uS\n"};         // 11    74 chars + 2 32(16) + 2 16(8) = 98 + (n 32x2) up to 24 overruns...
prog_char stopStamp[] PROGMEM = {  "%STOP AT\n"};      // used to stamp SD record when stopped by PC
prog_char startStamp[] PROGMEM = {  "%START AT\n"};    // used to stamp SD record when started by PC


boolean setupSDcard(char limit){

  if(!cardInit){
       if(!card.init(SPI_FULL_SPEED, SD_SS)) {
      Serial0.println("initialization failed. Things to check:");
      Serial0.println("* is a card is inserted?");
      //    card.init(SPI_FULL_SPEED, SD_SS);
      }
      else{
      Serial0.println("Wiring is correct and a card is present.");
      cardInit = true;
      }
      if (!volume.init(card)) { // Now we will try to open the 'volume'/'partition' - it should be FAT16 or FAT32
      Serial0.println("Could not find FAT16/FAT32 partition. Make sure you've formatted the card");
      return fileIsOpen;
      }
   }

  // use limit to determine file size
  switch(limit){
  case 'h':
    BLOCK_COUNT = 50; break;
  case 'a':
    BLOCK_COUNT = 512; break;
  case 'A':
    BLOCK_COUNT = BLOCK_5MIN; break;
  case 'S':
    BLOCK_COUNT = BLOCK_15MIN; break;
  case 'F':
    BLOCK_COUNT = BLOCK_30MIN; break;
  case 'G':
    BLOCK_COUNT = BLOCK_1HR; break;
  case 'H':
    BLOCK_COUNT = BLOCK_2HR; break;
  case 'J':
    BLOCK_COUNT = BLOCK_4HR; break;
  case 'K':
    BLOCK_COUNT = BLOCK_12HR; break;
  case 'L':
    BLOCK_COUNT = BLOCK_24HR; break;
  default: Serial0.println("invalid BLOCK count"); return fileIsOpen; break;
  }

    incrementFileCounter();
    openvol = root.openRoot(volume);
    openfile.remove(root, currentFileName); // if the file is over-writing, let it!

      if (!openfile.createContiguous(root, currentFileName, BLOCK_COUNT*512UL)) {
      Serial0.print("createfdContiguous fail"); cardInit = false;
      }//else{Serial0.print("got contiguous file...");delay(1);}
      // get the location of the file's blocks
      if (!openfile.contiguousRange(&bgnBlock, &endBlock)) {
      Serial0.print("get contiguousRange fail"); cardInit = false;
      }//else{Serial0.print("got file range...");delay(1);}
      // grab the Cache
      pCache = (uint8_t*)volume.cacheClear();
      // tell card to setup for multiple block write with pre-erase
      if (!card.erase(bgnBlock, endBlock)){ Serial0.println("erase block fail"); cardInit = false;
      }//else{Serial0.print("erased...");delay(1);}
      if (!card.writeStart(bgnBlock, BLOCK_COUNT)){ Serial0.println("writeStart fail"); cardInit = false;
      }else{ fileIsOpen = true; delay(1);}
      OBCI.csHigh(SD_SS);  // release the spi
      // initialize write-time overrun error counter and min/max wirte time benchmarks
      overruns = 0;
      maxWriteTime = 0;
      minWriteTime = 65000;
      byteCounter = 0;  // counter from 0 - 512
      blockCounter = 0; // counter from 0 - BLOCK_COUNT;
     if(fileIsOpen == true){  // send corresponding file name to controlling program
       Serial0.print("Corresponding SD file ");Serial0.println(currentFileName);sendEOT();
     }
     return fileIsOpen;
}

boolean closeSDfile(){
  if(fileIsOpen){
    OBCI.csLow(SD_SS);  // take spi
    card.writeStop();
    openfile.close();
    OBCI.csHigh(SD_SS);  // release the spi
    fileIsOpen = false;
    if(!is_running){ // verbosity. this also gets insterted as footer in openFile
      Serial0.print("Total Elapsed Time: ");Serial0.print(t);Serial0.println("mS"); delay(10);
      Serial0.print("Max write time: "); Serial0.print(maxWriteTime); Serial0.println(" uS"); delay(10);
      Serial0.print("Min write time: ");Serial0.print(minWriteTime); Serial0.println(" uS"); delay(10);
      Serial0.print("Overruns: "); Serial0.print(overruns); Serial0.println(); delay(10);
      if (overruns) {
        uint8_t n = overruns > OVER_DIM ? OVER_DIM : overruns;
        Serial0.println("fileBlock,micros");
        for (uint8_t i = 0; i < n; i++) {
          Serial0.print(over[i].block); Serial0.print(','); Serial0.println(over[i].micro);
        }
      }
    }
  }else{
    Serial0.println("no open file to close");
  }
  delay(100); // cool down
  return fileIsOpen;
}

void writeDataToSDcard(byte sampleNumber){
  boolean addComma = true;
  // convert 8 bit sampleCounter into HEX
  convertToHex(sampleNumber, 1, addComma);
  // convert 24 bit channelData into HEX
  for (int currentChannel = 0; currentChannel < 8; currentChannel++){
    convertToHex(OBCI.boardChannelDataInt[currentChannel], 5, addComma);
    if(OBCI.daisyPresent == false){
      if(currentChannel == 6){
        addComma = false;
        if(addAuxToSD || addAccelToSD) {addComma = true;}  // format CSV
      }
    }
  }
  if(OBCI.daisyPresent){
    for (int currentChannel = 0; currentChannel < 8; currentChannel++){
      convertToHex(OBCI.daisyChannelDataInt[currentChannel], 5, addComma);
      if(currentChannel == 6){
        addComma = false;
        if(addAuxToSD || addAccelToSD) {addComma = true;}  // format CSV
      }
    }
  }

  if(addAuxToSD == true){
    // convert auxData into HEX
    for(int currentChannel = 0; currentChannel <  3; currentChannel++){
      convertToHex(OBCI.auxData[currentChannel], 3, addComma);
      if(currentChannel == 1) addComma = false;
    }
    addAuxToSD = false;
  }// end of aux data log
  else if(addAccelToSD == true){  // if we have accelerometer data to log
    // convert 16 bit accelerometer data into HEX
    for (int currentChannel = 0; currentChannel < 3; currentChannel++){
      convertToHex(OBCI.axisData[currentChannel], 3, addComma);
      if(currentChannel == 1) addComma = false;
    }
    addAccelToSD = false;  // reset addAccelToSD
  }// end of accelerometer data log
   // add aux data logging...
}


void writeCache(){
    if(blockCounter > BLOCK_COUNT) return;
    uint32_t tw = micros();  // start block write timer
    OBCI.csLow(SD_SS);  // take spi
    if(!card.writeData(pCache)) {Serial0.println("block write fail");}   // write the block
    OBCI.csHigh(SD_SS);  // release spi
    tw = micros() - tw;      // stop block write timer
    if (tw > maxWriteTime) maxWriteTime = tw;  // check for max write time
    if (tw < minWriteTime) minWriteTime = tw;  // check for min write time
    if (tw > MICROS_PER_BLOCK) {      // check for overrun
      if (overruns < OVER_DIM) {
        over[overruns].block = blockCounter;
        over[overruns].micro = tw;
      }
      overruns++;
    }
    byteCounter = 0; // reset 512 byte counter for next block
    blockCounter++;    // increment BLOCK counter
    if(blockCounter == BLOCK_COUNT-1){
      t = millis() - t;
      stopRunning();
      OBCI.disable_accel();
      writeFooter();
    }
    if(blockCounter == BLOCK_COUNT){
      closeSDfile();
      BLOCK_COUNT = 0;
    }  // we did it!
}


void incrementFileCounter(){
  fileTens = EEPROM.read(0);
  fileOnes = EEPROM.read(1);
  // if it's the first time writing to EEPROM, seed the file number to '00'
  if(fileTens == 0xFF | fileOnes == 0xFF){
    fileTens = fileOnes = '0';
  }
  fileOnes++;   // increment the file name
  if (fileOnes == ':'){fileOnes = 'A';}
  if (fileOnes > 'F'){
    fileOnes = '0';         // hexify
    fileTens++;
    if(fileTens == ':'){fileTens = 'A';}
    if(fileTens > 'F'){fileTens = '0';fileOnes = '1';}
  }
  EEPROM.write(0,fileTens);     // store current file number in eeprom
  EEPROM.write(1,fileOnes);
  currentFileName[5] = fileTens;
  currentFileName[6] = fileOnes;
//  // send corresponding file name to controlling program
//  Serial0.print("Corresponding SD file ");Serial0.println(currentFileName);
}

void stampSD(boolean state){
  unsigned long time = millis();
  if(state){
    for(int i=0; i<10; i++){
      pCache[byteCounter] = pgm_read_byte_near(startStamp+i);
      byteCounter++;
      if(byteCounter == 512){
        writeCache();
      }
    }
  }
  else{
    for(int i=0; i<9; i++){
      pCache[byteCounter] = pgm_read_byte_near(stopStamp+i);
      byteCounter++;
      if(byteCounter == 512){
        writeCache();
      }
    }
  }
  convertToHex(time, 7, false);
}

void writeFooter(){
  for(int i=0; i<17; i++){
    pCache[byteCounter] = pgm_read_byte_near(elapsedTime+i);
    byteCounter++;
  }
  convertToHex(t, 7, false);

  for(int i=0; i<20; i++){
    pCache[byteCounter] = pgm_read_byte_near(minTime+i);
    byteCounter++;
  }
  convertToHex(minWriteTime, 7, false);

  for(int i=0; i<20; i++){
    pCache[byteCounter] = pgm_read_byte_near(maxTime+i);
    byteCounter++;
  }
  convertToHex(maxWriteTime, 7, false);

  for(int i=0; i<7; i++){
    pCache[byteCounter] = pgm_read_byte_near(overNum+i);
    byteCounter++;
  }
  convertToHex(overruns, 7, false);
  for(int i=0; i<11; i++){
    pCache[byteCounter] = pgm_read_byte_near(blockTime+i);
    byteCounter++;
  }
  if (overruns) {
    uint8_t n = overruns > OVER_DIM ? OVER_DIM : overruns;
    for (uint8_t i = 0; i < n; i++) {
      convertToHex(over[i].block, 7, true);
      convertToHex(over[i].micro, 7, false);
    }
  }
  for(int i=byteCounter; i<512; i++){
    pCache[i] = NULL;
  }
  writeCache();
}

//    CONVERT RAW BYTE DATA TO HEX FOR SD STORAGE
void convertToHex(long rawData, int numNibbles, boolean useComma){

  for (int currentNibble = numNibbles; currentNibble >= 0; currentNibble--){
    byte nibble = (rawData >> currentNibble*4) & 0x0F;
    if (nibble > 9){
      nibble += 55;  // convert to ASCII A-F
    }
    else{
      nibble += 48;  // convert to ASCII 0-9
    }
    pCache[byteCounter] = nibble;
    byteCounter++;
    if(byteCounter == 512){
      writeCache();
    }
  }
  if(useComma == true){
    pCache[byteCounter] = ',';
  }else{
    pCache[byteCounter] = '\n';
  }
  byteCounter++;
  if(byteCounter == 512){
    writeCache();
  }
}// end of byteToHex converter
